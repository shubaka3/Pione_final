// SPDX-License-Identifier: MIT
pragma solidity ^0.8.28;

import { Ownable } from "@openzeppelin/contracts/access/Ownable.sol";

import "./TYTAgriSupplyChain.sol";

/**
 * @title DemoFarmTraceabilityFactory
 * @dev A factory contract for managing companies and creating agriculture supply chain contracts
 * @notice This contract allows the owner to register companies and enables companies to create supply chain contracts
 */
contract DemoFarmTraceabilityFactory is Ownable {

    /**
     * @dev Struct representing a company in the system
     * @param id Unique identifier for the company (hash of registration number)
     * @param name Name of the company
     * @param wallet Ethereum wallet address associated with the company
     * @param registrationNumber Unique registration number of the company
     * @param isRegistered Boolean indicating if the company is currently registered
     */
    struct Company {
        bytes32 id;
        string name;
        address wallet;
        string registrationNumber;
        bool isRegistered;
    }

    /**
     * @dev Struct representing an agriculture supply chain contract
     * @param _companyId ID of the company that owns this supply chain
     * @param contractAddr Address of the deployed supply chain contract
     * @param owner Address of the supply chain owner
     * @param name Name of the supply chain
     * @param description Description of the supply chain
     * @param listIndex Index of this supply chain in the company's list
     * @param createdAt Timestamp when the supply chain was created
     */
    struct AgriSupplyChain {
        bytes32 _companyId;
        address contractAddr;
        address owner;
        string name;
        string description;
        uint listIndex;
        uint createdAt;
    }

    /// @dev Mapping from registration number (string) to company ID (bytes32)
    mapping(string => bytes32) private _companyIds;

    // Storage mappings
    
    /// @dev Mapping from company ID to Company struct
    mapping(bytes32 => Company) private companies;
    
    /// @dev Mapping from company ID to mapping of contract address to AgriSupplyChain struct
    mapping(bytes32 => mapping(address => AgriSupplyChain)) private agriSupplyChains;
    
    /// @dev Mapping from company ID to array of supply chain contract addresses
    mapping(bytes32 => address[]) private addressAgriSupplyChains;

    // Events
    
    /**
     * @dev Emitted when a new company is registered
     * @param companyId Unique identifier of the registered company
     * @param name Name of the company
     * @param registrationNumber Registration number of the company
     * @param owner Wallet address of the company
     * @param updateAt Timestamp when the company was registered
     */
    event CompanyRegistered(bytes32 indexed companyId, string name, string registrationNumber, address owner, uint updateAt);   
    
    /**
     * @dev Emitted when a company's information is modified
     * @param companyId Unique identifier of the modified company
     * @param name New name of the company
     * @param wallet New wallet address of the company
     * @param updateAt Timestamp when the company was modified
     */
    event CompanyModified(bytes32 indexed companyId, string name, address wallet, uint updateAt);
    
    /**
     * @dev Emitted when a company is deactivated
     * @param companyId Unique identifier of the deactivated company
     * @param registrationNumber Registration number of the deactivated company
     * @param updateAt Timestamp when the company was deactivated
     */
    event CompanyDeactivated(bytes32 indexed companyId, string registrationNumber, uint updateAt);
    
    /**
     * @dev Emitted when a new agriculture supply chain contract is created
     * @param contractAddr Address of the newly created supply chain contract
     * @param owner Address of the supply chain owner
     * @param name Name of the supply chain
     * @param index Index of the supply chain in the company's list
     * @param createAt Timestamp when the supply chain was created
     */
    event AgriSupplyChainCreated(address indexed contractAddr, address owner, string name, uint index, uint createAt);

    /**
     * @dev Constructor that sets the contract deployer as the owner
     */
    constructor() Ownable(msg.sender) {}

    /**
     * @dev Registers a new company with the provided details.
     * Only the contract owner can call this function.
     * @param _registrationNumber The unique registration number of the company.
     * @param _name The name of the company.
     * @param _wallet The wallet address associated with the company.
     * @return The unique company ID (bytes32).
     * @notice The company ID is generated by hashing the registration number
     * @notice Reverts if a company with the same registration number is already registered
     */
    function registerCompany(string calldata _registrationNumber, string calldata _name, address _wallet) external onlyOwner returns (bytes32) {
        
        require(bytes(_registrationNumber).length > 0, "Registration number cannot be empty");
        require(bytes(_name).length > 0, "Company name cannot be empty");
        require(_wallet != address(0), "Invalid wallet address");
     
        // Generate unique company ID from registration number
        bytes32 _companyId = keccak256(abi.encode(_registrationNumber));
        
        // Check if company is already registered
        require(!companies[_companyId].isRegistered, "Already registered");

        // Store company information
        companies[_companyId] = Company({
            id: _companyId,
            name: _name,
            wallet: _wallet,
            registrationNumber: _registrationNumber,
            isRegistered: true
        });
        _companyIds[_registrationNumber] = _companyId;

        // Emit registration event
        emit CompanyRegistered(_companyId, _name, _registrationNumber, _wallet, block.timestamp);   
        return _companyId;
    }

    /**
     * @dev Modifies the details of an existing company.
     * Only the contract owner can call this function.
     * @param _registrationNumber The registration number of the company to modify.
     * @param name The new name of the company.
     * @param wallet The new wallet address associated with the company.
     * @notice Reverts if the company is not registered
     */
    function modifyCompany(string calldata _registrationNumber, string calldata name, address wallet) external onlyOwner {

        require(bytes(_registrationNumber).length > 0, "Registration number cannot be empty");
        require(bytes(name).length > 0, "Company name cannot be empty");
        require(wallet != address(0), "Invalid wallet address");

        bytes32 _companyId = _companyIds[_registrationNumber];
        require(companies[_companyId].isRegistered, "Company not registered");
        
        // Update company information
        companies[_companyId].name = name;
        companies[_companyId].wallet = wallet;

        emit CompanyModified(_companyId, name, wallet, block.timestamp);
    }

    /**
     * @dev Deactivates an existing company by setting its registration status to false.
     * Only the contract owner can call this function.
     * @param _registrationNumber The registration number of the company to deactivate.
     * @notice Reverts if the company is not registered
     * @notice This does not delete the company data, just marks it as inactive
     */
    function deactiveCompany(string calldata _registrationNumber) external onlyOwner {
        bytes32 _companyId = _companyIds[_registrationNumber];
        require(companies[_companyId].isRegistered, "Company not registered");
        
        // Deactivate the company
        companies[_companyId].isRegistered = false;

        emit CompanyDeactivated(_companyId, _registrationNumber, block.timestamp);
    }

    /**
     * @dev Retrieves the information of a registered company.
     * @param _registrationNumber The registration number of the company.
     * @return name The name of the company.
     * @return wallet The wallet address of the company.
     * @return registrationNumber The registration number of the company.
     * @return isRegistered The registration status of the company.
     * @notice Reverts if the company is not registered
     */
    function getCompanyInfo(string calldata _registrationNumber) external view 
        returns (string memory name, address wallet, string memory registrationNumber, bool isRegistered) 
    {
        bytes32 _companyId = _companyIds[_registrationNumber];
        require(companies[_companyId].isRegistered, "Company is not registered");

        Company memory company = companies[_companyId];

        return (company.name, company.wallet, company.registrationNumber, company.isRegistered);
    }

    /**
     * @dev Creates a new supply chain contract and associates it with the caller.
     * @param _registrationNumber The registration number of the company creating the supply chain.
     * @param _name The name of the supply chain.
     * @param _desc The description of the supply chain.
     * @return The address of the newly created SupplyChainAgri contract.
     * @notice Only the registered company's wallet or the contract owner can call this function
     * @notice Creates a new TYTAgriSupplyChain contract instance
     */
    function createSupplyChain(string calldata _registrationNumber, string calldata _name, string calldata _desc) external returns (address) {
        bytes32 _companyId = _companyIds[_registrationNumber];
        address companyWallet = companies[_companyId].wallet;
        require(companyWallet == msg.sender || owner() == msg.sender, "Sender does not have permission");
        
        // Create new TYTAgriSupplyChain contract
        TYTAgriSupplyChain newChain = new TYTAgriSupplyChain(_companyId, companyWallet);
        address chainAddress = address(newChain);
        
        // Get index for the new supply chain
        uint index = addressAgriSupplyChains[_companyId].length;
        
        // Store supply chain information
        agriSupplyChains[_companyId][chainAddress] = AgriSupplyChain({
            _companyId: _companyId,
            contractAddr: chainAddress,
            owner: companyWallet,
            name: _name,
            description: _desc,
            listIndex: index,
            createdAt: block.timestamp
        });
        
        // Add contract address to company's list
        addressAgriSupplyChains[_companyId].push(chainAddress);

        emit AgriSupplyChainCreated(chainAddress, msg.sender, _name, index, block.timestamp);
        return address(newChain);
    }

    /**
     * @dev Retrieves all supply chain contract addresses owned by a specific company.
     * @param _registrationNumber The registration number of the company.
     * @return An array of supply chain contract addresses belonging to the company.
     * @notice Returns an empty array if the company has no supply chains
     */
    function getListAgriSupplyChain(string calldata _registrationNumber) external view returns (address[] memory) {
        bytes32 _companyId = keccak256(abi.encode(_registrationNumber));
        require(companies[_companyId].isRegistered, "Company not registered");
        // Generate company ID from registration number
        return addressAgriSupplyChains[_companyId];
    }

    /**
     * @notice Retrieves the agricultural supply chain information for a specific company and supply chain address.
     * @dev The company is identified by its registration number, which is hashed to obtain the company ID.
     *      The function checks if the company is registered and if it has any associated supply chains.
     * @param _registrationNumber The registration number of the company.
     * @param agriSupplyAddress The address of the agricultural supply chain contract.
     * @return The AgriSupplyChain struct containing the supply chain information for the specified address.
     */
    function getAgriSupplyChainInfo(string calldata _registrationNumber, address agriSupplyAddress) 
        external 
        view 
        returns (AgriSupplyChain memory) 
    {
        bytes32 _companyId = keccak256(abi.encode(_registrationNumber));
        require(companies[_companyId].isRegistered, "Company not registered");
        require(addressAgriSupplyChains[_companyId].length > 0, "No supply chains found for this company");

        return agriSupplyChains[_companyId][agriSupplyAddress];
    }

    /**
     * @dev Utility function to get company ID from registration number.
     * @param _registrationNumber The registration number of the company.
     * @return The company ID (bytes32 hash of the registration number).
     * @notice This is a pure function that can be called without any state changes
     * @notice Useful for external applications to generate company IDs
     */
    function getCompanyIdfromTaxcode(string calldata _registrationNumber) external pure returns(bytes32) {
        require(bytes(_registrationNumber).length > 0, "Registration number cannot be empty");
        return keccak256(abi.encode(_registrationNumber));
    }
}